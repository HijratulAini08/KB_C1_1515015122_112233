DOMAINS
 nama,jender,pekerjaan,benda,alasan,zat = symbol
 umur=integer

PREDICATES 
 nondeterm orang(nama, umur, jender, pekerjaan)
 nondeterm selingkuh(nama, nama)
 terbunuh_dengan(nama, benda)
 terbunuh(nama)
 nondeterm pembunuh(nama)
 motif(alasan)
 ternodai(nama, zat)
 milik(nama, benda)
 nondeterm cara_kerja_mirip(benda, benda)
 nondeterm kemungkinan_milik(nama, benda)
 nondeterm dicurigai(nama)

/* * * Fakta-fakta tentang pembunuhan * * */
CLAUSES
 orang(budi,55,m,tukang_kayu).
 orang(aldi,25,m,pemain_sepak_bola).
 orang(aldi,25,m,tukang_jagal).
 orang(joni,25,m,pencopet).

 selingkuh(ina,joni).
 selingkuh(ina,budi).
 selingkuh(siti,joni).

 terbunuh_dengan(siti,pentungan).
 terbunuh(siti).

 motif(uang).
 motif(cemburu).
 motif(dendam).

 ternodai(budi, darah). 
 ternodai(siti, darah). 
 ternodai(aldi, lumpur).
 ternodai(joni, coklat).
 ternodai(ina, coklat).

 milik(budi,kaki_palsu).
 milik(joni,pistol).
 
 /* * * Basis Pengetahuan * * */
 cara_kerja_mirip(kaki_palsu, pentungan). 
 cara_kerja_mirip(balok, pentungan).
 cara_kerja_mirip(gunting, pisau).
 cara_kerja_mirip(sepatu_bola, pentungan).
 
 kemungkinan_milik(X,sepatu_bola):- 
 	orang(X,_,_,pemain_sepak_bola). 
  
 kemungkinan_milik(X,gunting):-
	 orang(X,_,_,pekerja_salon). 
 
 kemungkinan_milik(X,Benda):-
	 milik(X,Benda).

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* dicurigai semua orang yang memiliki senjata yang *
* kerjanya mirip dengan senjata penyebab siti terbunuh. *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 dicurigai(X):- 
	 terbunuh_dengan(siti,Senjata) ,
	 cara_kerja_mirip(Benda,Senjata) ,
	 				  
	 kemungkinan_milik(X,Benda).
	
/* * * * * * * * * * * * * * * * * * * * * * * * * *
* dicurigai laki-laki yang selingkuh dengan siti. *
* * * * * * * * * * * * * * * * * * * * * * * * * */
 dicurigai(X):- 
	motif(cemburu),
	orang(X,_,m,_),
	selingkuh(siti,X).
	
/* * * * * * * * * * * * * * * * * * * * * * *
* dicurigai perempuan yang selingkuh dengan *
* laki-laki yang juga selingkuh dengan siti *
* * * * * * * * * * * * * * * * * * * * * * */
 dicurigai(X):-
	motif(cemburu),
	orang(X,_,f,_),
	selingkuh(X,Lakilaki),
	selingkuh(siti,Lakilaki).
	
/* * * * * * * * * * * * * * * * * * * * * * * * * * *
* dicurigai pencopet yang mempunyai motif uang. *
* * * * * * * * * * * * * * * * * * * * * * * * * * */
 dicurigai(X):-
	motif(uang),
	orang(X,_,_,pencopet).

%tempat predicate pembunuh, dimana predicate tersebut harus dibuktikan kebenarannya. 
 pembunuh(Pembunuh):- 
	orang(Pembunuh,_,_,_), 
	terbunuh(Terbunuh), 
	Terbunuh <> Pembunuh, /* Bukan bunuh diri */
	dicurigai(Pembunuh),
	ternodai(Pembunuh,Zat),
	ternodai(Terbunuh,Zat).

GOAL
 pembunuh(X). 

 
 /*
 Analisis :
 1. ketika program dijalankan, maka visual prolog akan mengecek predicate dari goal tersebut. ternyata predicate dari goal adalah pembunuh yang memiliki variable bebas yaitu X.
 2. kemudian visual prolog akan mencari pada klausa, dimana prolog akan mencari fakta/rule yang cocok dengan goal.maka prolog menemukan pembunuh(Pembunuh).
 karna var.bebas dapat di unifikasi ke var. manapun. sehingga var.X di unifikasi dengan var.Pembunuh. jadi X=Pembunuh.
 lalu visual prolog akan melanjutkan pengujian, dimana prolog akan mengecek subgoal, yaitu:
 	subgoal ke- :
	1. pembunuh adalah pembunuh dan pembunuh adalah orang.
	maka prolog akan mencari head/fakta yang sesuai dengan subgoal, prolog akan mencari dari bagian atas klausa sampai bagian bawah dari klausa.
	maka ditemukan fakta, sehingga var.Pembunuh akan di ikat ke nilai budi, dan subgoal berikutnya akan dijalankan.
	2. terbunuh adalah terbunuh.
	lalu visual prolog akan mengecek klausa dari atas sampai ke bawah. dan menememukan fakta. sehingga var.terbunuh akan di ikat ke nilai siti. dan akan menjalankan subgoal berikutnya
	3. terbunuh tidak sama dengan pembunuh
	yang menandakan bahwa terbunuh tidak bunuh diri.
	4. pembunuh merupakan yang dicurigai
	visual prolog sekarang akan memulai pencarian untuk mencocokkan klausa dicurigai.karena var.X telah diunifikasi ke var.Pembunuh dan var.Pembunuh telah diikat dengan nilai "Budi", maka visual prolog akan memenuhi rule dicurigai.
		1. siti terbunuh dengan senjata, dimana senjata merupakan variable bebas.
		maka prolog akan mengecek klausa dari atas sampai kebawah untuk mencocokkan dengan subgoal. sehingga var.Senjata diikat ke nilai "pentungan".
		2. benda cara_kerja_mirip senjata
		karena senjata telah diikat dengan pentungan. maka prolog akan mencocokkan pada klausa cara_kerja_mirip(kaki_palsu, pentungan), dan var.Benda mengikat nilai "kaki palsu"
		3. X kemungkinan milik benda. 
		karena benda telah diikat dengan nilai kaki palsu, dan x= pembunuh=budi. dan pada klausa kemungkinan_milik bersifat identik,maka untuk rule dicurigai selesai. 
	5. pembunuh ternodai zat
	selanjutnya prolog akan memulai pencarian untuk melakukan pencocokan pemanggilan ternodai.karena pembunuh telah diikat dengan nilai "budi", maka pemanggilan sebenarnya adalah ternodai(budi,zat)
	pada pencocokan pertama prolog akan mencocokkan dengan klausa ternodai(budi,darah), dan karena nilai argumen tersebut cocok dengan subgoal yang memanggil, maka var. Zat diikat pada nilai "darah".
	6. terbunuh ternodai zat
	subgoal terakhir yaitu terbunuh ternodai zat,karena terbunuh telah diikat dengan siti, maka prolog akan melakukan pencocokan dengan klausa. maka var.Zat diikat pada nilai darah. sehingga siti ternodai darah.
 
 3. setelah semua rule terpenuhi, maka visual prolog akan menampilkan pembunuh, dan pembunuh adalah BUDI.
 */